plugins {
  id "com.moowork.node" version "1.2.0"
}

description = 'Mimic UI'
jar.enabled = false

defaultTasks 'clean', 'build'

node {
	version = '8.9.4'
	download = true
	workDir = file("${project.projectDir}/node")
	nodeModulesDir = file("${project.projectDir}")
}

task cleanDist(type: Delete) {
	delete 'dist'
}

task cleanNode(type: Delete) {
	delete 'node'
	delete 'node_modules'
}

task createAppBundleZip(type: Zip, dependsOn: yarn_build) {

    description= 'Bundle app distribution as ZIP artefact'

    from file('dist')
    destinationDir file("${project.buildDir}")
}

configurations {
	appBundle
}

artifacts {
	appBundle createAppBundleZip
}

clean.dependsOn cleanDist
yarn_build.dependsOn yarn_install

yarn_install.inputs.file file("package.json")
yarn_install.outputs.dir file("node_modules")
yarn_build.inputs.files file("package.json")
yarn_build.inputs.files file("tsconfig.json")
yarn_build.inputs.dir file("src")
yarn_build.outputs.dir file("dist")
