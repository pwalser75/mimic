import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.2.RELEASE'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
        classpath 'com.netflix.nebula:gradle-aggregate-javadocs-plugin:3.0.1'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'nebula-aggregate-javadocs'

def appTitle = 'Mimic'
def appVendor = 'frostnova.ch'

allprojects {
    defaultTasks 'clean', 'build'
}

subprojects {
    apply plugin: 'java'

    group = 'ch.frostnova.mimic'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
    }

    jar { duplicatesStrategy = 'exclude' }

    compileJava.options.encoding = 'UTF-8'
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    def dateTimeFormat = DateTimeFormatter.ofPattern("yyyy-MM-dd HH:mm:ss")
    def buildDate = LocalDateTime.now()

    plugins.withType(JavaPlugin) {
        jar {
            manifest {
                attributes(
                        "Name": project.name,
                        "Version": project.version,
                        "Build-Date": dateTimeFormat.format(buildDate),
                        "Implementation-Title": appTitle,
                        "Implementation-Version": project.version,
                        "Implementation-Vendor": appVendor
                )
            }
        }
    }
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

ext {
    springBootVersion = '2.0.2.RELEASE'

    libs = [
            json              : [
                    "com.fasterxml.jackson.core:jackson-annotations:2.9.5",
                    "com.fasterxml.jackson.core:jackson-databind:2.9.5",
                    "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.5"
            ],
            testbase          : [
                    "junit:junit:4.12",
                    "org.mockito:mockito-core:2.18.3"
            ],
            springBoot        : [
                    "org.springframework.boot:spring-boot-starter-jersey:${springBootVersion}",
                    "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}",
                    "javax.xml.bind:jaxb-api:2.3.0",
                    "javax.activation:activation:1.1.1"
            ],
            springBootActuator: [
                    "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}",
                    "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
            ],
            springBootTest    : [
                    "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
            ],
            liquibase         : "org.liquibase:liquibase-core:3.6.1",
            h2                : "com.h2database:h2:1.4.197",
            frostnova         : [
                    check : "ch.frostnova:check:1.0.0-RC2",
                    keygen: "ch.frostnova:keygen:1.0.0-RC1"
            ]
    ]
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6.0'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

task start(dependsOn: ':mimic-web:bootRun') {
    group = 'Start/Run'
    description = 'Start the Mimic server'
}

